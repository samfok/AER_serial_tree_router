// Defines N input logic gates
import "channel.act";

export
template <pint N>
defproc AND (bool i[N], o) {
    // AND tree with 2 and 3 input NANDs and 2 input NORs
    [ N=1 ->
        i[0] = o;
    [] N=2 ->
        bool _o;
        prs {
            i[0] & i[1] => _o-
            _o => o-
        }
    [] N=3 ->
        bool _o;
        prs {
            i[0] & i[1] & i[2] => _o-
            _o => o-
        }
    [] N=4 ->
        bool _x01, _x23;
        prs {
            i[0] & i[1] => _x01-
            i[2] & i[3] => _x23-
            _x01 | _x23 => o-
        }
    [] N=5 ->
        bool _x012, _x34;
        prs {
            i[0] & i[1] & i[2] => _x012-
            i[3] & i[4] => _x34-
            _x012 | _x34 => o-
        }
    [] N>=6 ->
        pint k=N/6;
        AND<k>     and0(i[0..k-1],);
        AND<k>     and1(i[k..2*k-1],);
        AND<k>     and2(i[2*k..3*k-1],);
        AND<k>     and3(i[3*k..4*k-1],);
        AND<k>     and4(i[4*k..5*k-1],);
        AND<N-5*k> and5(i[5*k..N-1],);
        bool _x012, _x345;
        prs {
            and0.o & and1.o & and2.o => _x012-
            and3.o & and4.o & and5.o => _x345-
            _x012 | _x345 => o-
        }
    ]
}

export
template <pint N>
defproc AND_LINE (bool i[N], o) {
    // Logical AND implemented as a line of AND gates
    // Implemented with 2 input NANDs
    [ N = 1 ->
        i[0] = o;
    [] N >= 2 ->
        AND_LINE<N-1> and(i[0..N-2],);
        bool _o;
        prs {
            and.o & i[N-1] => _o-
            _o => o-
        }
    ]
}

export
template <pint N>
defproc OR (bool i[N], o) {
    // Logical OR implemented with as few transistors as possible
    // Implemented as an OR tree with 2 and 3 input NANDs and 2 input NORs
    // We don't use 3 input NORs because the pullup chain would be too slow
    [ N=1 ->
        i[0] = o;
    [] N=2 ->
        bool _o;
        prs {
            i[0] | i[1] => _o-
            _o => o-
        }
    [] N=3 ->
        bool _x0, _x12;
        prs {
            i[0] => _x0-
            i[1] | i[2] => _x12-
            _x0 & _x12 => o-
        }
    [] N=4 ->
        bool _x01, _x23;
        prs {
            i[0] | i[1] => _x01-
            i[2] | i[3] => _x23-
            _x01 & _x23 => o-
        }
    [] N=5 ->
        bool _x01, _x23, _x4;
        prs {
            i[0] | i[1] => _x01-
            i[2] | i[3] => _x23-
            i[4] => _x4-
            _x01 & _x23 & _x4 => o-
        }
    [] N>=6 ->
        pint k=N/6;
        OR<k>     or0(i[0..k-1],);
        OR<k>     or1(i[k..2*k-1],);
        OR<k>     or2(i[2*k..3*k-1],);
        OR<k>     or3(i[3*k..4*k-1],);
        OR<k>     or4(i[4*k..5*k-1],);
        OR<N-5*k> or5(i[5*k..N-1],);
        bool _x01, _x23, _x45;
        prs {
            or0.o | or1.o => _x01-
            or2.o | or3.o => _x23-
            or4.o | or5.o => _x45-
            _x01 & _x23 & _x45 => o-
        }
    ]
}

export
template <pint N>
defproc OR_LINE (bool i[N], o) {
    // Logical OR implemented as line of OR gates
    // Implemented with 2 input NORs
    [ N = 1 ->
        i[0] = o;
    [] N >= 2 ->
        OR_LINE<N-1> or(i[0..N-2],);
        bool _o;
        prs {
            or.o | i[N-1] => _o-
            _o => o-
        }
    ]
}

export
template <pint N>
defproc C_TREE (bool i[N], o) {
    [ N=1 ->
        i[0] = o;
    [] N=2 -> 
        bool _o;
        prs {
            i[0] & i[1] -> _o-
            ~i[0] & ~i[1] -> _o+
            _o => o-
        }
    [] N>2 ->
        C_TREE<N/2> c_tree0(i[0..N/2-1],);
        C_TREE<N-N/2> c_tree1(i[N/2..N-1],);
        bool _o;
        prs {
            c_tree0.o & c_tree1.o -> _o-
            ~c_tree0.o & ~c_tree1.o -> _o+
            _o => o-
        }
    ]
}

export
template <pint M, N>
defproc VN (c1of<N> d[M]; bool o) {
    // detects data validity/neutrality on d
    OR<N> or[M];
    (; m : M :
        (; n : N :
            or[m].i[n] = d[m].n[n];
        )
    )

    C_TREE<M> c_tree(,o);

    (; k : M :
        or[k].o = c_tree.i[k];
    )
}

template <pint M, N>
defproc Mx1ofN_WIRES (c1of<N> d[M]; bool o[M*N]) {
    // Connect a Mx1ofN channel to NN wires
    pint MN = M*N;
    (; n : N :
        d[0].n[n] = o[n];
    )
    [ M > 1 ->
        Mx1ofN_WIRES<M-1, N> w(d[1..M-1], o[N..MN-1]);
    ]
}

export
template <pint M, N, NN>
defproc N_DETECT (c1of<N> d[M]; bool o) {
    // detects data neutrality on d
    bool x[NN];
    Mx1ofN_WIRES<M, N> wires(d, x);
    OR<NN> or(x, o);
}
